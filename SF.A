         MACRO
&LABEL   SF    &FLAG,&BAD              SET FLAG BIT(S)
         AIF   (T'&BAD EQ 'O').GOOD
         MNOTE 12,'TOO MANY OPERANDS SPECIFIED, BAD=&BAD'
         MEXIT
.GOOD    AIF   (T'&FLAG NE 'O').BEGIN
         MNOTE 12,'AN OPERAND IS REQUIRED'
         MEXIT
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.* This is a simple macro to make it easier to accurately manipulate *
.* a bit flag definition just by  the equated bit mask name -- i.e., *
.* the macro does not need to know  the name of the actual flag byte *
.* nor the flag byte even have to have a name at all.                *
.*                                                                   *
.* As presented by the late Dr. John Ehrman of IBM at SHARE.         *
.*                                                                   *
.* Example: SF FLAG3              SET THIS FLAG (SET ON)             *
.* +        OI FLAG3,L'FLAG3           (macro expansion)             *
.*                                                                   *
.* The idea is,  you can have a  definition similar to either of the *
.* following.  The actual flag byte does not need a name but you can *
.* specify one if you want it  to  show up in the cross-reference or *
.* if you want the equated  bit  masks  to follow the flag byte def- *
.* inition.   The equated bit masks then take on the location of the *
.* flag byte and the length of  the  equated  bit mask is set to the *
.* bit mask, itself.                                                 *
.*                                                                   *
.* FLAG1    EQU   *,B'00000001'                                      *
.* FLAG2    EQU   *,B'00000010'                                      *
.* FLAG3    EQU   *,B'00000100'                                      *
.* FLAG4    EQU   *,B'00001000'                                      *
.* FLAG5    EQU   *,B'00010000'                                      *
.* FLAG6    EQU   *,B'00100000'                                      *
.* FLAG7    EQU   *,B'01000000'                                      *
.* FLAG8    EQU   *,B'10000000'                                      *
.*          DS    BL1           MY FLAG BYTE                         *
.*                                                                   *
.* MYFLAG   DS    BL1           MY FLAG BYTE                         *
.* FLAG1    EQU   MYFLAG,B'00000001'                                 *
.* FLAG2    EQU   MYFLAG,B'00000010'                                 *
.* FLAG3    EQU   MYFLAG,B'00000100'                                 *
.* FLAG4    EQU   MYFLAG,B'00001000'                                 *
.* FLAG5    EQU   MYFLAG,B'00010000'                                 *
.* FLAG6    EQU   MYFLAG,B'00100000'                                 *
.* FLAG7    EQU   MYFLAG,B'01000000'                                 *
.* FLAG8    EQU   MYFLAG,B'10000000'                                 *
.*                                                                   *
.* However,  the DF macro was  created  to  insure that the bit mask *
.* flags used are defined  correctly  (i.e.,  according to the above *
.* rules).  This means that this macro's &FLAG operand must point to *
.* a bit mask that was generated by the DF macro.  See that macro.   *
.*                                                                   *
.* NOTE:  See also the  TF  and CF macros.   Also,  setting multiple *
.* flags at the same time  is  not currently supported -- unless you *
.* use the DF macro to generate  an additional combined flag that is *
.* specifically for that purpose.                                    *
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.BEGIN   ANOP
         AIF   ('Flag' EQ SYSATTRP('&FLAG')).GEN_IT
         MNOTE 12,'FLAG NOT GENERATED BY DF MACRO'
         MEXIT
.GEN_IT  ANOP
&LABEL   OI    &FLAG,L'&FLAG           SET FLAG BIT(s)
         MEND
